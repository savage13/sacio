%{
#include <stdio.h>
#include "sacio.h"
%}
struct hid { char *name; int  type; int  id; };

%includes
%ignore-case
%language=ANSI-C
%struct-type
%omit-struct-type
%define lookup-function-name sac_keyword_to_header
%define initializer-suffix ,-1,-1

%%
delta,	SAC_FLOAT_TYPE, SAC_DELTA
depmin,	SAC_FLOAT_TYPE, SAC_DEPMIN
depmax,	SAC_FLOAT_TYPE, SAC_DEPMAX
scale,	SAC_FLOAT_TYPE, SAC_SCALE
odelta,	SAC_FLOAT_TYPE,	SAC_ODELTA
b,	    SAC_FLOAT_TYPE,	SAC_B
e,	    SAC_FLOAT_TYPE,	SAC_E
o,	    SAC_FLOAT_TYPE,	SAC_O
a,	    SAC_FLOAT_TYPE,	SAC_A
fmt,	  SAC_FLOAT_TYPE,	SAC_FMT
t0,	    SAC_FLOAT_TYPE,	SAC_T0
t1,	    SAC_FLOAT_TYPE,	SAC_T1
t2,	    SAC_FLOAT_TYPE,	SAC_T2
t3,	    SAC_FLOAT_TYPE,	SAC_T3
t4,	    SAC_FLOAT_TYPE,	SAC_T4
t5,	    SAC_FLOAT_TYPE,	SAC_T5
t6,	    SAC_FLOAT_TYPE,	SAC_T6
t7,	    SAC_FLOAT_TYPE,	SAC_T7
t8,	    SAC_FLOAT_TYPE,	SAC_T8
t9,	    SAC_FLOAT_TYPE,	SAC_T9
f,	    SAC_FLOAT_TYPE,	SAC_F
resp0,	SAC_FLOAT_TYPE,	SAC_RESP0
resp1,	SAC_FLOAT_TYPE,	SAC_RESP1
resp2,	SAC_FLOAT_TYPE,	SAC_RESP2
resp3,	SAC_FLOAT_TYPE,	SAC_RESP3
resp4,	SAC_FLOAT_TYPE,	SAC_RESP4
resp5,	SAC_FLOAT_TYPE,	SAC_RESP5
resp6,	SAC_FLOAT_TYPE,	SAC_RESP6
resp7,	SAC_FLOAT_TYPE,	SAC_RESP7
resp8,	SAC_FLOAT_TYPE,	SAC_RESP8
resp9,	SAC_FLOAT_TYPE,	SAC_RESP9
stla,	  SAC_FLOAT_TYPE,	SAC_STLA
stlo,	  SAC_FLOAT_TYPE,	SAC_STLO
stel,	  SAC_FLOAT_TYPE,	SAC_STEL
stdp,	  SAC_FLOAT_TYPE,	SAC_STDP
evla,	  SAC_FLOAT_TYPE,	SAC_EVLA
evlo,	  SAC_FLOAT_TYPE,	SAC_EVLO
evel,	  SAC_FLOAT_TYPE,	SAC_EVEL
evdp,	  SAC_FLOAT_TYPE,	SAC_EVDP
mag,	  SAC_FLOAT_TYPE,	SAC_MAG
user0,	SAC_FLOAT_TYPE,	SAC_USER0
user1,	SAC_FLOAT_TYPE,	SAC_USER1
user2,	SAC_FLOAT_TYPE,	SAC_USER2
user3,	SAC_FLOAT_TYPE,	SAC_USER3
user4,	SAC_FLOAT_TYPE,	SAC_USER4
user5,	SAC_FLOAT_TYPE,	SAC_USER5
user6,	SAC_FLOAT_TYPE,	SAC_USER6
user7,	SAC_FLOAT_TYPE,	SAC_USER7
user8,	SAC_FLOAT_TYPE,	SAC_USER8
user9,	SAC_FLOAT_TYPE,	SAC_USER9
dist,	  SAC_FLOAT_TYPE,	SAC_DIST
az,	    SAC_FLOAT_TYPE,	SAC_AZ
baz,	  SAC_FLOAT_TYPE,	SAC_BAZ
gcarc,	SAC_FLOAT_TYPE,	SAC_GCARC
sb,     SAC_FLOAT_TYPE, SAC_SB
sdelta, SAC_FLOAT_TYPE, SAC_SDELTA
depmen, SAC_FLOAT_TYPE, SAC_DEPMEN
cmpaz,	SAC_FLOAT_TYPE,	SAC_CMPAZ
cmpinc,	SAC_FLOAT_TYPE,	SAC_CMPINC
xminimum,	SAC_FLOAT_TYPE,	SAC_XMIN
xmaximum,	SAC_FLOAT_TYPE,	SAC_XMAX
yminimum,	SAC_FLOAT_TYPE,	SAC_YMIN
ymaximum,	SAC_FLOAT_TYPE,	SAC_YMAX
adjtm,   SAC_FLOAT_TYPE, SAC_UN64
fhdr65,  SAC_FLOAT_TYPE, SAC_UN65
fhdr66,  SAC_FLOAT_TYPE, SAC_UN66
fhdr67,  SAC_FLOAT_TYPE, SAC_UN67
fhdr68,  SAC_FLOAT_TYPE, SAC_UN68
fhdr69,  SAC_FLOAT_TYPE, SAC_UN69
fhdr70,  SAC_FLOAT_TYPE, SAC_UN70
unused6,  SAC_FLOAT_TYPE, SAC_UN64
unused7,  SAC_FLOAT_TYPE, SAC_UN65
unused8,  SAC_FLOAT_TYPE, SAC_UN66
unused9,  SAC_FLOAT_TYPE, SAC_UN67
unused10, SAC_FLOAT_TYPE, SAC_UN68
unused11, SAC_FLOAT_TYPE, SAC_UN69
unused12, SAC_FLOAT_TYPE, SAC_UN70
nzyear,	SAC_INT_TYPE,	SAC_YEAR
nzjday,	SAC_INT_TYPE,	SAC_DAY
nzhour,	SAC_INT_TYPE,	SAC_HOUR
nzmin,	SAC_INT_TYPE,	SAC_MIN
nzsec,	SAC_INT_TYPE,	SAC_SEC
nzmsec,	SAC_INT_TYPE,	SAC_MSEC
nvhdr,	SAC_INT_TYPE,	SAC_HDR
norid,	SAC_INT_TYPE,	SAC_ORID
nevid,	SAC_INT_TYPE,	SAC_EVID
npts,	  SAC_INT_TYPE,	SAC_NPTS
nsnpts,	SAC_INT_TYPE,	SAC_NSNPTS
nwfid,	SAC_INT_TYPE,	SAC_WFID
nxsize,	SAC_INT_TYPE,	SAC_NX
nysize,	SAC_INT_TYPE,	SAC_NY
xsize,	SAC_INT_TYPE,	SAC_NX
ysize,	SAC_INT_TYPE,	SAC_NY
nhdr15, SAC_INT_TYPE, SAC_UN85
unused15, SAC_INT_TYPE, SAC_UN85
iftype,	SAC_ENUM_TYPE,	SAC_FILE_TYPE
idep,	  SAC_ENUM_TYPE,	SAC_DEP_TYPE
iztype,	SAC_ENUM_TYPE,	SAC_ZERO_TIME
ihdr4,  SAC_ENUM_TYPE,  SAC_UN89
unused16,  SAC_ENUM_TYPE,  SAC_UN89
iinst,	SAC_ENUM_TYPE,	SAC_INST_TYPE
istreg,	SAC_ENUM_TYPE,	SAC_STREG
ievreg,	SAC_ENUM_TYPE,	SAC_EVREG
ievtyp, SAC_ENUM_TYPE,  SAC_EVENT_TYPE
iqual,	SAC_ENUM_TYPE,	SAC_QUAL
isynth,	SAC_ENUM_TYPE,	SAC_SYNTH
imagtyp,	SAC_ENUM_TYPE,	SAC_MAG_TYPE
imagsrc,	SAC_ENUM_TYPE,	SAC_MAG_SRC
ihdr13, SAC_ENUM_TYPE, SAC_UN98
ihdr14, SAC_ENUM_TYPE, SAC_UN99
ihdr15, SAC_ENUM_TYPE, SAC_UN100
ihdr16, SAC_ENUM_TYPE, SAC_UN101
ihdr17, SAC_ENUM_TYPE, SAC_UN102
ihdr18, SAC_ENUM_TYPE, SAC_UN103
ihdr19, SAC_ENUM_TYPE, SAC_UN104
ihdr20, SAC_ENUM_TYPE, SAC_UN105
unused19, SAC_ENUM_TYPE, SAC_UN98
unused20, SAC_ENUM_TYPE, SAC_UN99
unused21, SAC_ENUM_TYPE, SAC_UN100
unused22, SAC_ENUM_TYPE, SAC_UN101
unused23, SAC_ENUM_TYPE, SAC_UN102
unused24, SAC_ENUM_TYPE, SAC_UN103
unused25, SAC_ENUM_TYPE, SAC_UN104
unused26, SAC_ENUM_TYPE, SAC_UN105
leven,	SAC_BOOL_TYPE,	SAC_EVEN
lpspol,	SAC_BOOL_TYPE,	SAC_POLARITY
lovrok,	SAC_BOOL_TYPE,	SAC_OVERWRITE
lcalda,	SAC_BOOL_TYPE,	SAC_CALC_DIST_AZ
lhdr5, SAC_BOOL_TYPE,  SAC_UN110
unused27, SAC_BOOL_TYPE,  SAC_UN110
kstnm,	SAC_STRING_TYPE,	SAC_STA
kevnm,	SAC_LONG_STRING_TYPE,	SAC_EVENT
kevnmc,	SAC_LONG_STRING_TYPE,	SAC_EVENT
khole,	SAC_STRING_TYPE,	SAC_HOLE
ko,	    SAC_STRING_TYPE,	SAC_KO
ka,	    SAC_STRING_TYPE,	SAC_KA
kt0,	  SAC_STRING_TYPE,	SAC_KT0
kt1,	  SAC_STRING_TYPE,	SAC_KT1
kt2,	  SAC_STRING_TYPE,	SAC_KT2
kt3,	  SAC_STRING_TYPE,	SAC_KT3
kt4,	  SAC_STRING_TYPE,	SAC_KT4
kt5,	  SAC_STRING_TYPE,	SAC_KT5
kt6,	  SAC_STRING_TYPE,	SAC_KT6
kt7,	  SAC_STRING_TYPE,	SAC_KT7
kt8,	  SAC_STRING_TYPE,	SAC_KT8
kt9,	  SAC_STRING_TYPE,	SAC_KT9
kf,	    SAC_STRING_TYPE,	SAC_KF
kuser0,	SAC_STRING_TYPE,	SAC_KUSER0
kuser1,	SAC_STRING_TYPE,	SAC_KUSER1
kuser2,	SAC_STRING_TYPE,	SAC_KUSER2
kcmpnm,	SAC_STRING_TYPE,	SAC_CHA
knetwk,	SAC_STRING_TYPE,	SAC_NET
kdatrd,	SAC_STRING_TYPE,	SAC_DATRD
kinst,	SAC_STRING_TYPE,	SAC_INST
kzdate,	SAC_AUX_TYPE,	SAC_DATE
kztime,	SAC_AUX_TYPE,	SAC_TIME
kstcmp,	SAC_AUX_TYPE,	SAC_STCMP
nzmonth, SAC_AUX_TYPE, SAC_MONTH
nzday, SAC_AUX_TYPE, SAC_MONTH_DAY
reftime, SAC_AUX_TYPE, SAC_DATE_TIME
filename,	SAC_AUX_TYPE,	SAC_FILENAME
amarker,	SAC_AUX_TYPE,	SAC_AMARKER
omarker,	SAC_AUX_TYPE,	SAC_OMARKER
fmarker,	SAC_AUX_TYPE,	SAC_FMARKER
t0marker,	SAC_AUX_TYPE,	SAC_T0MARKER
t1marker,	SAC_AUX_TYPE,	SAC_T1MARKER
t2marker,	SAC_AUX_TYPE,	SAC_T2MARKER
t3marker,	SAC_AUX_TYPE,	SAC_T3MARKER
t4marker,	SAC_AUX_TYPE,	SAC_T4MARKER
t5marker,	SAC_AUX_TYPE,	SAC_T5MARKER
t6marker,	SAC_AUX_TYPE,	SAC_T6MARKER
t7marker,	SAC_AUX_TYPE,	SAC_T7MARKER
t8marker,	SAC_AUX_TYPE,	SAC_T8MARKER
t9marker,	SAC_AUX_TYPE,	SAC_T9MARKER
%%

#ifdef __TESTING__
//struct hid { char *name; int  type; int  id; };
int
main() {
       struct hid *h = NULL;
       h = sac_keyword_to_header("delta", 5);
       printf("%s %d %d\n", h->name, h->type, h->id);
       h = sac_keyword_to_header("delt", 4);
       if(h) {
          printf("%s %d %d\n", h->name, h->type, h->id);
       }
}
#endif
